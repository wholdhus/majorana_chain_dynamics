{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "72eab359",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Functions for Creating Hamiltonian and Running Simulation\n",
    "\n",
    "using ITensors, ITensorMPS\n",
    "using LinearAlgebra\n",
    "using DataFrames, CSV\n",
    "using Plots\n",
    "\n",
    "function make_H0(L, sites, bc)\n",
    "    osI = OpSum()\n",
    "    for j in 1:L\n",
    "        osI -= \"Z\", j\n",
    "    end   \n",
    "    for j in 1:L-1\n",
    "        osI -= \"X\", j, \"X\", j+1\n",
    "    end\n",
    "    \n",
    "    if bc == \"PBC\"\n",
    "        osI -= \"X\", L, \"X\", 1\n",
    "    elseif bc == \"APBC\"\n",
    "        osI += \"X\", L, \"X\", 1\n",
    "    end\n",
    "\n",
    "    return MPO(osI, sites)\n",
    "end\n",
    "\n",
    "function make_H3(L, sites, bc)\n",
    "    os3 = OpSum()\n",
    "    for j in 1:L-2\n",
    "        os3 += \"Z\", j, \"X\", j+1, \"X\", j+2\n",
    "        os3 += \"X\", j, \"X\", j+1, \"Z\", j+2\n",
    "    end\n",
    "\n",
    "    if bc == \"PBC\"\n",
    "        os3 += \"Z\", L-1, \"X\", L, \"X\", 1\n",
    "        os3 += \"X\", L-1, \"X\", L, \"Z\", 1\n",
    "        os3 += \"Z\", L, \"X\", 1, \"X\", 2\n",
    "        os3 += \"X\", L, \"X\", 1, \"Z\", 2\n",
    "    elseif bc == \"APBC\"\n",
    "        os3 -= \"Z\", L-1, \"X\", L, \"X\", 1\n",
    "        os3 += \"X\", L-1, \"X\", L, \"Z\", 1\n",
    "        os3 += \"Z\", L, \"X\", 1, \"X\", 2\n",
    "        os3 -= \"X\", L, \"X\", 1, \"Z\", 2\n",
    "    end\n",
    "\n",
    "    return MPO(os3, sites)\n",
    "end\n",
    "\n",
    "function local_H(t0, g, s1, s2, s3; signs=[1.0,1.0,1.0,1.0])\n",
    "\n",
    "    H = 2*t0*(-signs[1]*op(\"Z\", s1)*op(\"Id\", s2)*op(\"Id\", s3))\n",
    "\n",
    "    if signs[2] != 0\n",
    "        H += 2*t0*(-signs[2]*op(\"X\", s1)*op(\"X\", s2)*op(\"Id\", s3))\n",
    "    end\n",
    "\n",
    "    if signs[3] != 0\n",
    "        H += g*signs[3]*op(\"Z\", s1)*op(\"X\", s2)*op(\"X\", s3)\n",
    "    end\n",
    "    if signs[4] != 0\n",
    "        H += g*signs[4]*op(\"X\", s1)*op(\"X\", s2)*op(\"Z\", s3)\n",
    "    end\n",
    "\n",
    "    return H\n",
    "end\n",
    "\n",
    "function build_trotter_gates(sites, tau, t0, g, bc)\n",
    "\n",
    "    L = length(sites)\n",
    "    gates = ITensor[]\n",
    "    \n",
    "    for j in 1:L-2\n",
    "        H = local_H(t0, g, sites[j], sites[j+1], sites[j+2])\n",
    "        push!(gates, exp(-im*tau/2 * H))\n",
    "    end\n",
    "    \n",
    "    append!(gates, reverse(gates))\n",
    "    return gates\n",
    "end\n",
    "nothing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "63d591fd",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "After sweep 1 energy=-8.320148853431087  maxlinkdim=4 maxerr=9.24E-16 time=0.007\n",
      "After sweep 2 energy=-8.320148853431087  maxlinkdim=4 maxerr=1.15E-15 time=0.004\n",
      "After sweep 3 energy=-8.320148853431089  maxlinkdim=4 maxerr=8.13E-16 time=0.006\n",
      "Energy difference less than 1.0e-8, stopping DMRG\n",
      "Ground state energy: -8.320148853431089\n",
      "Overlap: 1.0\n",
      "Final Maximum Bond Dimension: 4\n"
     ]
    }
   ],
   "source": [
    "# Running Time-Indepent DMRG\n",
    "\n",
    "L = 4\n",
    "bc = \"OBC\" # Boundary Conditions: Open = \"OBC\", Periodic = \"PBC\", Anti-Periodic = \"APBC\"\n",
    "t0 = 1\n",
    "g = 1\n",
    "parity = 1\n",
    "nsweeps = 20\n",
    "cutoff = 1e-8\n",
    "maxdim = 100\n",
    "dmrg_tol = 1e-8\n",
    "\n",
    "sites = siteinds(\"S=1/2\", L, conserve_szparity=true)\n",
    "\n",
    "H0 = make_H0(L, sites, bc)\n",
    "H3 = make_H3(L, sites, bc)\n",
    "H = 2*t0*H0 + g*H3\n",
    "\n",
    "state = [\"Up\" for _ in 1:L]\n",
    "if parity == -1\n",
    "    state[1] = \"Dwn\"\n",
    "end\n",
    "\n",
    "psi = random_mps(sites, state, linkdims=4)\n",
    "\n",
    "observer = DMRGObserver(energy_tol=dmrg_tol)\n",
    "maxdim = [10, 50, 100, 500, 1000, 5000, 10000, 50000]\n",
    "\n",
    "E0, psi0 = dmrg(H, psi; nsweeps, maxdim, cutoff, observer, outputlevel=1, noise=[0.0], eigsolve_krylovdim=10)\n",
    "\n",
    "println(\"Ground state energy: \", E0)\n",
    "println(\"Overlap: \", inner(psi0, psi0))\n",
    "println(\"Final Maximum Bond Dimension: \", maxlinkdim(psi0))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0c8143cd",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Final Overlap: 0.6614750201439072\n"
     ]
    }
   ],
   "source": [
    "# Creating Time-Dependent Wave Function\n",
    "\n",
    "omega = 100\n",
    "periods = 40\n",
    "steps_per_period = 10\n",
    "\n",
    "T = 2Ï€/omega\n",
    "tau = T/steps_per_period\n",
    "ts = [tau*(n-0.5) for n in 1:steps_per_period]\n",
    "\n",
    "driven_t0(t) = t0*cos(omega*t)\n",
    "\n",
    "gate_sets = [build_trotter_gates(sites, tau, driven_t0(t), g, bc) for t in ts]\n",
    "\n",
    "steps = periods*steps_per_period + 1\n",
    "times = [tau*i for i in 0:(steps-1)]\n",
    "\n",
    "psi_t = copy(psi0)\n",
    "\n",
    "overlaps = zeros(Float64, steps)\n",
    "maxdims = zeros(Int, steps)\n",
    "\n",
    "overlaps[1] = abs2(inner(psi_t, psi0))\n",
    "maxdims[1] = maxlinkdim(psi_t)\n",
    "\n",
    "for i in 2:steps\n",
    "    gates = gate_sets[mod1(i-1, steps_per_period)]\n",
    "    psi_t = apply(gates, psi_t; cutoff=cutoff, maxdim=maximum(maxdim))\n",
    "    normalize!(psi_t)\n",
    "    overlaps[i] = abs2(inner(psi_t, psi0))\n",
    "    maxdims[i] = maxlinkdim(psi_t)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e3fa67ed",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Autocorrelation\n",
    "\n",
    "operator = \"Z\"\n",
    "ind = 1\n",
    "\n",
    "O = op(operator, sites[ind])\n",
    "\n",
    "psi_t = copy(psi0)\n",
    "phi_t = apply(O, psi0)\n",
    "\n",
    "ac = zeros(ComplexF64, steps)\n",
    "ac[1] = inner(phi_t, apply(O, psi_t))\n",
    "\n",
    "for i in 2:steps\n",
    "    gates = gate_sets[mod1(i-1, steps_per_period)]\n",
    "    psi_t = apply(gates, psi_t; cutoff=cutoff, maxdim=maximum(maxdim))\n",
    "    normalize!(psi_t)\n",
    "\n",
    "    phi_t = apply(gates, phi_t; cutoff=cutoff, maxdim=maximum(maxdim))\n",
    "    normalize!(phi_t)\n",
    "\n",
    "    ac[i] = inner(phi_t, apply(O, psi_t))\n",
    "end\n",
    "\n",
    "real_ac = real.(ac);\n",
    "im_ac = imag.(ac);\n",
    "abs_ac = abs.(ac);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 31,
   "id": "97e9fb9d",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "File Created: L4_OBC_g1_omega100_N40.csv\n"
     ]
    }
   ],
   "source": [
    "# Print to .csv File\n",
    "\n",
    "output = DataFrame(\n",
    "    \"time\" => times,\n",
    "    \"overlap\" => overlaps,\n",
    "    \"maxdim\" => maxdims,\n",
    "    \"real_ac\"=> real_ac,\n",
    "    \"im_ac\" => im_ac\n",
    "    )\n",
    "\n",
    "fname = \"L$(L)_$(bc)_g$(g)_omega$(omega)_N$(periods).csv\"\n",
    "CSV.write(fname, output)\n",
    "\n",
    "println(\"File Created: \", fname)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
